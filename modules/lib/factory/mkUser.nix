{self, ...}: {
  config.flake.factory.user = username: isAdmin: {
    nixos."${username}" = {
      lib,
      pkgs,
      ...
    }: {
      users.users."${username}" = {
        isNormalUser = true;
        home = "/home/${username}";
        createHome = true;
        extraGroups = lib.optionals isAdmin [
          "wheel"
        ];
        shell = pkgs.fish;
      };
      programs.fish.enable = true;

      home-manager.users."${username}" = {
        imports = [
          self.modules.homeManager."${username}"
        ];
      };
    };

    darwin."${username}" = {
      lib,
      pkgs,
      ...
    }: {
      users.users."${username}" = {
        home = "/Users/${username}";
        shell = pkgs.fish;
      };

      home-manager.users."${username}" = {
        imports = [
          self.modules.homeManager."${username}"
        ];
      };

      system.primaryUser = lib.mkIf isAdmin "${username}";

      programs.fish.enable = true;
    };

    homeManager."${username}" = {
      home.username = "${username}";
    };
  };
}
